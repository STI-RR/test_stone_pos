plugins {
    id "com.android.application"
    id "kotlin-android"
    id "kotlin-parcelize"
    // START: FlutterFire Configuration
    id 'com.google.gms.google-services'
    id 'com.google.firebase.firebase-perf'
    id 'com.google.firebase.crashlytics'
    // END: FlutterFire Configuration
    id "dev.flutter.flutter-gradle-plugin"
}

def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader('UTF-8') { reader ->
        localProperties.load(reader)
    }
}

def flutterVersionCode = localProperties.getProperty('flutter.versionCode')
if (flutterVersionCode == null) {
    flutterVersionCode = '1'
}

def flutterVersionName = localProperties.getProperty('flutter.versionName')
if (flutterVersionName == null) {
    flutterVersionName = '1.0'
}

def keystoreProperties = new Properties()
def keystorePropertiesFile = rootProject.file('key.properties')
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

apply from: '../positivo/positivo-signing-config.gradle'
android {
    namespace "br.com.stirr.easyfood.app.easyfood_app"
    compileSdk = flutter.compileSdkVersion
    ndkVersion = flutter.ndkVersion

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildFeatures {
        buildConfig true
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    defaultConfig {
        applicationId "br.com.stirr.easyfood.app.easyfood_app"
        minSdkVersion 23 // flutter.minSdkVersion
        targetSdk = flutter.targetSdkVersion
        versionCode = flutter.versionCode
        versionName = flutter.versionName
        buildConfigField "String", "FLAVOR", "\"default\""
    }

    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile keystoreProperties['storeFile'] ? file(keystoreProperties['storeFile']) : null
            storePassword keystoreProperties['storePassword']
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            ndk {
                debugSymbolLevel 'FULL'
            }
        }
        debug {
            signingConfig signingConfigs.debug
            debuggable true
        }
    }

    flavorDimensions "app"

    productFlavors {
        garcom {
            dimension "app"
            applicationIdSuffix ".garcom"
            versionNameSuffix "-garcom"
            buildConfigField "String", "FLAVOR", "\"garcom\""
        }
        stone {
            dimension "app"
            applicationIdSuffix ".stone"
            versionNameSuffix "-stone"
            buildConfigField "String", "FLAVOR", "\"stone\""
            minSdkVersion 22
        }
    }
}

flutter {
    source '../..'
}

dependencies {
    // Dependências para o flavor 'stone'
    stoneImplementation "br.com.stone:stone-sdk:$stone_sdk_version"
    stoneImplementation "br.com.stone:stone-sdk-posandroid:$stone_sdk_version"
    stoneImplementation "br.com.stone:stone-sdk-posandroid-ingenico:$stone_sdk_version"
    stoneImplementation "br.com.stone:stone-sdk-posandroid-sunmi:$stone_sdk_version"
    stoneImplementation "br.com.stone:stone-sdk-posandroid-gertec:$stone_sdk_version"
    stoneImplementation "br.com.stone:stone-sdk-posandroid-positivo:$stone_sdk_version"
    stoneImplementation(platform("com.google.firebase:firebase-bom:33.1.1"))
    stoneImplementation("com.google.firebase:firebase-crashlytics")
    stoneImplementation("com.google.firebase:firebase-analytics")
    // Dependências para debugar o flavor 'stone'
    debugImplementation "com.github.tony19:logback-android:2.0.0"
    debugImplementation 'br.com.stone.sdk.android:debugmode:4.0.3'
}